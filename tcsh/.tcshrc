if ($?USERNAME) then                                                    
if ($USERNAME == "root") then
echo "Permission denied"
kill -9 $$Â¬
exit
endif
endif

set prompt="%T %n@%m %c%# "
set autolist=ambiguous
set complete=1
set showdots
set color
set filec
set autocorrect
set history=1000
set savehist=(1000 merge)
set histdup=erase
set ignoreeof
set listjobs
set notify
set rmstar
limit coredumpsize 0

bindkey -k up history-search-backward
bindkey -k down history-search-forward

setenv EDITOR vim
setenv LANG en_US.UTF-8
setenv PATH ~/bin:$PATH

alias vi vim
alias l ls
alias :q 'fg >& /dev/null ;exit'
alias cd.. cd ..
alias vi.tcshrc vi ~/.tcshrc

setenv COLOR_FLAG `ls --color >& /dev/null && echo -n "--color" || echo -n "-G"`
alias ls ls -F $COLOR_FLAG
alias la ls -AF $COLOR_FLAG
alias ll ls -lAF $COLOR_FLAG
unsetenv COLOR_FLAG

complete cd 'p/1/d/'
complete apt-cache 'p/1/(search)/'
complete git 'p/1/(pull push add commit status log)/' \
	'n/commit/(-am)/'
complete {which,where,sudo,strace,exec,trace} 'p/1/c/' \
	'n/apt-get/(update upgrade install)/'
